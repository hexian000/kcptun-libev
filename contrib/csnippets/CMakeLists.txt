add_library(csnippets STATIC)
target_include_directories(csnippets BEFORE PUBLIC ${CMAKE_CURRENT_SOURCE_DIR})
target_compile_options(csnippets PUBLIC "-include${CMAKE_CURRENT_BINARY_DIR}/config.h")
target_compile_options(csnippets PRIVATE -pedantic -Wall -Wextra)

if(FORCE_POSIX)
    target_compile_definitions(csnippets PRIVATE _POSIX_C_SOURCE=200809L)
else()
    target_compile_definitions(csnippets PRIVATE _GNU_SOURCE)
endif()
target_compile_definitions(csnippets PUBLIC MCACHE_STATS=0 SLOG_MT_SAFE=0)

add_subdirectory(algo)
add_subdirectory(math)
add_subdirectory(net)
add_subdirectory(os)
add_subdirectory(utils)

include(CheckSymbolExists)
list(APPEND CMAKE_REQUIRED_DEFINITIONS "-D_POSIX_C_SOURCE=200809L" "-D_XOPEN_SOURCE=700")
check_symbol_exists(clock_gettime "time.h" HAVE_CLOCK_GETTIME)
check_symbol_exists(timespec_get "time.h" HAVE_TIMESPEC_GET)
check_symbol_exists(syslog "syslog.h" HAVE_SYSLOG)
check_symbol_exists(gmtime_r "time.h" HAVE_GMTIME_R)
check_symbol_exists(localtime_r "time.h" HAVE_LOCALTIME_R)
check_symbol_exists(wcwidth "wchar.h" HAVE_WCWIDTH)
check_symbol_exists(backtrace "execinfo.h" HAVE_BACKTRACE)
check_symbol_exists(backtrace_symbols "execinfo.h" HAVE_BACKTRACE_SYMBOLS)

function(find_backtrace)
    find_path(LIBBACKTRACE_INCLUDE_DIR NAMES backtrace.h)
    find_library(LIBBACKTRACE_LIBRARY NAMES libbacktrace.a)
    if(EXISTS "${LIBBACKTRACE_INCLUDE_DIR}/backtrace-supported.h")
        file(STRINGS "${LIBBACKTRACE_INCLUDE_DIR}/backtrace-supported.h" BACKTRACE_SUPPORTED_STR REGEX "^#define BACKTRACE_SUPPORTED ([^\n]*)$")
    endif()
    if((${BACKTRACE_SUPPORTED_STR} MATCHES "#define BACKTRACE_SUPPORTED 1") AND (EXISTS ${LIBBACKTRACE_LIBRARY}))
        message(STATUS "libbacktrace: ${LIBBACKTRACE_LIBRARY}")
        set(WITH_LIBBACKTRACE TRUE PARENT_SCOPE)
        return()
    endif()
    if(NOT BUILD_STATIC AND NOT LINK_STATIC_LIBS)
        find_path(LIBUNWIND_INCLUDE_DIR NAMES libunwind.h)
        find_library(LIBUNWIND_LIBRARY NAMES unwind)
        find_library(LIBLZMA_LIBRARY NAMES lzma)
        if((EXISTS ${LIBUNWIND_INCLUDE_DIR}) AND (EXISTS ${LIBUNWIND_LIBRARY}) AND (EXISTS ${LIBLZMA_LIBRARY}))
            message(STATUS "libunwind: ${LIBUNWIND_LIBRARY}")
            message(STATUS "liblzma: ${LIBLZMA_LIBRARY}")
            set(LIBUNWIND_LIBRARY "${LIBUNWIND_LIBRARY};${LIBLZMA_LIBRARY}")
            set(WITH_LIBUNWIND TRUE PARENT_SCOPE)
            return()
        endif()
    endif()
endfunction(find_backtrace)

if(CMAKE_BUILD_TYPE STREQUAL "Debug" OR CMAKE_BUILD_TYPE STREQUAL "RelWithDebInfo")
    set(WITH_DEBUGINFO TRUE)
    find_backtrace()
    if(WITH_LIBBACKTRACE)
        target_include_directories(csnippets PRIVATE ${LIBBACKTRACE_INCLUDE_DIR})
        target_link_libraries(csnippets PRIVATE ${LIBBACKTRACE_LIBRARY})
    elseif(WITH_LIBUNWIND)
        target_include_directories(csnippets PRIVATE ${LIBUNWIND_INCLUDE_DIR})
        target_link_libraries(csnippets PUBLIC ${LIBUNWIND_LIBRARY})
    endif()
endif()

# find systemd
if(ENABLE_SYSTEMD)
    find_path(SYSTEMD_INCLUDE_DIR NAMES systemd/sd-daemon.h)
    find_library(SYSTEMD_LIBRARY NAMES systemd)
    if((EXISTS ${SYSTEMD_INCLUDE_DIR}) AND (EXISTS ${SYSTEMD_LIBRARY}))
        set(WITH_SYSTEMD TRUE)
        message(STATUS "systemd: ${SYSTEMD_LIBRARY}")
        target_include_directories(csnippets PRIVATE ${SYSTEMD_INCLUDE_DIR})
        target_link_libraries(csnippets PUBLIC ${SYSTEMD_LIBRARY})
    else()
        message(WARNING "systemd not found")
    endif()
endif()

if(CMAKE_C_COMPILER_ID STREQUAL "GNU")
    set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -ffile-prefix-map=${CMAKE_CURRENT_SOURCE_DIR}/=")
elseif(CMAKE_C_COMPILER_ID MATCHES "Clang")
    set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -ffile-compilation-dir=${CMAKE_CURRENT_SOURCE_DIR}")
endif()

configure_file(config.h.in config.h ESCAPE_QUOTES)
